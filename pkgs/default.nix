# SPDX-License-Identifier: MIT

self: pkgs: {
  # Overlay lib
  lib = pkgs.lib.extend (import ../lib);

  # First argument is NIX_STATE_DIR which is where we init the dumped database
  nix_init_db =
    pkgs.writeScriptBin "nix_init_db" # bash
      ''
        #! ${pkgs.runtimeShell}
        NSD="$1"
        shift
        export USER nobody
        nix-store --option store local --dump-db "$@" | NIX_STATE_DIR="$NSD" nix-store --load-db --option store local
      '';

  nixkube = pkgs.python3Packages.callPackage ./nixkube {
    inherit (self)
      csi-proto-python
      cri-proto-python
      nri-proto-python
      grpclib-nri
      kr8s
      nri-wait
      ;
    coreutils = pkgs.pkgsStatic.coreutils;
  };

  # kluctl = pkgs.kluctl.override {
  #   python310 = pkgs.python3;
  # };

  stdNix = pkgs.nix;
  nix = pkgs.nix.overrideAttrs (oldAttrs: {
    doCheck = false;
    doInstallCheck = false;
  });

  grpclib-ttrpc = pkgs.python3Packages.callPackage ./grpclib-ttrpc {
    inherit (self) ttrpc-proto-python;
  };
  grpclib-nri = pkgs.python3Packages.callPackage ./grpclib-nri {
    inherit (self) grpclib-ttrpc nri-proto-python;
  };
  csi-proto-python = pkgs.python3Packages.callPackage ./csi-proto-python { };
  cri-proto-python = pkgs.python3Packages.callPackage ./cri-proto-python { };
  nri-proto-python = pkgs.python3Packages.callPackage ./nri-proto-python { };
  ttrpc-proto-python = pkgs.python3Packages.callPackage ./ttrpc-proto-python { };
  python-jsonpath = pkgs.python3Packages.callPackage ./python-jsonpath.nix { };
  kr8s = pkgs.python3Packages.callPackage ./kr8s.nix { inherit (self) python-jsonpath; };
  shellous = pkgs.python3Packages.callPackage ./shellous.nix { };

  # NRI wait Python application for OCI hooks
  # Runs inside chroot(/var/lib/nix-csi), uses pyzmq for communication
  nri-wait = pkgs.python3Packages.callPackage ./nri-wait { };

  ci-debug = pkgs.callPackage ./ci-debug { inherit pkgs; };

  pynixd =
    let
      path =
        if builtins.pathExists ../../pynixd then
          ../../pynixd
        else
          fetchTree {
            type = "github";
            owner = "lillecarl";
            repo = "pynixd";
            rev = "35d7fe3813a3801d94362cbe1aebfdefc34e29cc"; # develop @ 2026-05-28
          }; # pinned to avoid floating asyncssh→cryptography version mismatch
      # pynixd's default.nix overrides asyncssh to fetch from ronf/asyncssh main
      # (no rev pin), pulling v2.23.1 which requires cryptography>=48.0.1.
      # Our nixpkgs only has cryptography 46.0.4.
      # Fix: patch pynixd source to use nixpkgs asyncssh (2.22.0) directly
      # instead of the floating GitHub override.
      patchedPath =
        if builtins.isPath path then
          path
        else
          pkgs.runCommand "pynixd-patched" { } ''
            cp -r ${path} $out
            chmod -R +w $out
            # Remove the asyncssh overrideAttrs block (lines from opening paren
            # through closing paren) and replace with plain asyncssh
            ${pkgs.gnused}/bin/sed -i '/(python\.pkgs\.asyncssh\.overrideAttrs/,/})/c\      python.pkgs.asyncssh' $out/default.nix
          '';
    in
    (import patchedPath {
      inherit pkgs;
    }).library;
  pynixd-nixkube = pkgs.python3Packages.callPackage ./pynixd-nixkube {
    inherit (self) pynixd kr8s;
    inherit (pkgs) dockerTools;
  };
}
